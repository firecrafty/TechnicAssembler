apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = app_name
group = package_group
version = app_version
sourceCompatibility = 1.7
targetCompatibility = 1.7

mainClassName="${main_class}"
jar {
    classifier = ''
    manifest.mainAttributes(
            "Main-Class": "${main_class}",
            "Created-By": "${System.getProperty('java.vm.version')} + (${System.getProperty('java.vm.vendor')})",
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
    )
}
signing {
    sign configurations.archives
}
signArchives {
    onlyIf { !project.hasProperty('ci') }
}
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
                name app_name
                packaging 'jar'
                description "TechnicAssembler is a Java utility that simplifies the packaging of Minecraft modpacks for distribution on the Technic Platform"
                url 'https://github.com/firecrafty/TechnicAssembler'
                
                scm {
                    url 'scm:git@github.com:firecrafty/TechnicAssembler.git'
                    connection 'scm:git@github.com:firecrafty/TechnicAssembler.git'
                    developerConnection 'scm:git@github.com:firecrafty/TechnicAssembler.git'
                }
                
                licenses {
                    license {
                        name 'GNU General Public License (GPL), Version 3.0'
                        url 'http://www.gnu.org/licenses/gpl.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'firecrafty'
                        name 'Ryan Blue'
                    }
                }
                
                
            }
                
        }
    }
}
javadoc {
    title = "TechnicAssembler v" + app_version
    options.memberLevel = JavadocMemberLevel.PRIVATE
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}
artifacts {
    archives jar
    
    archives javadocJar
    archives sourcesJar
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}
